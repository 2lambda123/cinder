InlinerEliminationTest
---
InlineFunctionCalls
Simplify
BeginInlinedFunctionElimination
---
Simple
---
def foo():
    # Chosen by fair dice roll. Guaranteed to be random.
    return 4

def test():
    return foo()
---
fun jittestmodule:test {
  bb 0 {
    v2:OptObject = LoadGlobalCached<0; "foo">
    v3:MortalFunc[function:0xdeadbeef] = GuardIs<0xdeadbeef> v2 {
      Descr 'LOAD_GLOBAL: foo'
    }
    v9:Object = LoadField<func_code@16, Object, borrowed> v3
    v10:MortalCode["foo"] = GuardIs<0xdeadbeef> v9 {
    }
    v7:MortalLongExact[4] = LoadConst<MortalLongExact[4]>
    Return v7
  }
}
---
InlineMultipleFunctions
---
def foo():
    return 3

def bar():
    return 4

def test():
    return foo() + bar()
---
fun jittestmodule:test {
  bb 0 {
    v5:OptObject = LoadGlobalCached<0; "foo">
    v6:MortalFunc[function:0xdeadbeef] = GuardIs<0xdeadbeef> v5 {
      Descr 'LOAD_GLOBAL: foo'
    }
    v16:Object = LoadField<func_code@16, Object, borrowed> v6
    v17:MortalCode["foo"] = GuardIs<0xdeadbeef> v16 {
    }
    v14:MortalLongExact[3] = LoadConst<MortalLongExact[3]>
    v8:OptObject = LoadGlobalCached<1; "bar">
    v9:MortalFunc[function:0xdeadbeef] = GuardIs<0xdeadbeef> v8 {
      Descr 'LOAD_GLOBAL: bar'
    }
    v22:Object = LoadField<func_code@16, Object, borrowed> v9
    v23:MortalCode["bar"] = GuardIs<0xdeadbeef> v22 {
    }
    v20:MortalLongExact[4] = LoadConst<MortalLongExact[4]>
    UseType<LongExact> v14
    UseType<LongExact> v20
    v24:LongExact = LongBinaryOp<Add> v14 v20 {
      FrameState {
        NextInstrOffset 10
      }
    }
    Return v24
  }
}
---
BeginEndWithMemoryEffectsNotRemoved
---
def add(x, y):
  return x + y

def test():
    return add(3, 4)
---
fun jittestmodule:test {
  bb 0 {
    v4:OptObject = LoadGlobalCached<0; "add">
    v5:MortalFunc[function:0xdeadbeef] = GuardIs<0xdeadbeef> v4 {
      Descr 'LOAD_GLOBAL: add'
    }
    v6:MortalLongExact[3] = LoadConst<MortalLongExact[3]>
    v7:MortalLongExact[4] = LoadConst<MortalLongExact[4]>
    v19:Object = LoadField<func_code@16, Object, borrowed> v5
    v20:MortalCode["add"] = GuardIs<0xdeadbeef> v19 {
    }
    BeginInlinedFunction<jittestmodule:add> {
      NextInstrOffset 8
    }
    UseType<LongExact> v6
    UseType<LongExact> v7
    v21:LongExact = LongBinaryOp<Add> v6 v7 {
      FrameState {
        NextInstrOffset 6
        Locals<2> v6 v7
      }
    }
    EndInlinedFunction
    Return v21
  }
}
---
